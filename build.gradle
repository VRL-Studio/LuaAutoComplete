apply plugin: 'java'
apply plugin: 'me.champeau.gradle.antlr4'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'eclipse'
apply plugin: 'com.jfrog.artifactory'

version = '0.2-SNAPSHOT'
group = 'edu.gcsc.lua'

if (System.env['TRAVIS'] != null) {
    project.ext.set ("buildInfo.build.number", System.env.TRAVIS_BUILD_NUMBER)
    project.ext.set ("buildInfo.build.name", System.env.TRAVIS_BUILD_ID)
} else {
    project.ext.set ("buildInfo.build.number", '0' )
    project.ext.set ("buildInfo.build.name", rootProject.name+"-local")
}


jar.dependsOn compileJava
compileJava.dependsOn antlr4 

sourceCompatibility = '1.6'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

compileJava {
   
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

antlr4 {
    output=file('src/main/java/edu/gcsc/lua/grammar')
    
    extraArgs =['-package','edu.gcsc.lua.grammar']
}

artifacts {
    archives sourcesJar
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

repositories {
    mavenCentral()
    
    mavenLocal()
    
    maven {
        name 'jcenter'
        url  'http://jcenter.bintray.com'
    }

    // You may define additional repositories, or even remove "mavenCentral()".
    // Read more about repositories here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:repositories
}

buildscript {
    repositories {
        maven {
            name 'JFrog OSS snapshot repo'
            url  'https://oss.jfrog.org/oss-snapshot-local/'
        }
        
        // hopefully fixes #1 (https://github.com/turpid-monkey/LuaCompletionProvider/issues/1)
        // jcenter()
        maven {
            name 'jcenter'
            url  'http://jcenter.bintray.com'
        }
        
        maven {
            url 'http://oss.jfrog.org/artifactory/plugins-release'    
        }
    }

    dependencies {
        classpath 'me.champeau.gradle:antlr4-gradle-plugin:0.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '3.0.1')
    }
}

dependencies {
    
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile group: 'org.easymock', name: 'easymock', version: '3.3'
    
    compile (group: 'org.antlr', name: 'antlr4', version: '4.2')
    compile (group: 'org.antlr', name: 'antlr4', version: '4.2', classifier: 'sources')

}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = "turpid-monkey"
            password = System.properties['bintrayKey']
            maven = true
        }
        defaults {
            publications ('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            maven = true
             
        }
    }
}

