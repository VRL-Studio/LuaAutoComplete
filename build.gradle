apply plugin: 'java'
apply plugin: 'me.champeau.gradle.antlr4'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'eclipse'
apply plugin: 'com.jfrog.bintray'

version = '0.1'
group = 'edu.gcsc.lua'

jar.dependsOn compileJava
compileJava.dependsOn antlr4 

sourceCompatibility = '1.6'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

compileJava {
   
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

antlr4 {
    output=file('src/main/java/edu/gcsc/lua/grammar')
    
    extraArgs =['-package','edu.gcsc.lua.grammar']
}

artifacts {
    archives sourcesJar
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

bintray {
    user = "turpid-monkey"
    key = ""
    publications = ['mavenJava'] 
    publish = true
    pkg {
        repo = 'maven'
        name = 'edu.gcsc.lua'
        desc = 'A library to generate auto-completion proposals from LUA scripts.'
        websiteUrl = 'https://github.com/turpid-monkey/LuaAutoComplete'
        issueTrackerUrl = 'https://github.com/turpid-monkey/LuaAutoComplete/issues'
        vcsUrl = 'https://github.com/turpid-monkey/LuaAutoComplete.git'
        licenses = ['BSD 2-clause']
        labels = ['lua', 'auto-completion']
        attributes= ['plat': ['linux', 'osx', 'windows']]
        publicDownloadNumbers = true
        version {
            name = '0.1' //Optional logical version name
            desc = 'alpha-version'
            vcsTag = 'master'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

repositories {
    mavenCentral()
    
    mavenLocal()
    
    maven {
        name 'jcenter'
        url  'http://jcenter.bintray.com'
    }

    // You may define additional repositories, or even remove "mavenCentral()".
    // Read more about repositories here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:repositories
}

buildscript {
    repositories {
        maven {
            name 'JFrog OSS snapshot repo'
            url  'https://oss.jfrog.org/oss-snapshot-local/'
        }
        
        // hopefully fixes #1 (https://github.com/turpid-monkey/LuaCompletionProvider/issues/1)
        // jcenter()
        maven {
            name 'jcenter'
            url  'http://jcenter.bintray.com'
        }
    }

    dependencies {
        classpath 'me.champeau.gradle:antlr4-gradle-plugin:0.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
    }
}

dependencies {
    
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile group: 'org.easymock', name: 'easymock', version: '3.3'
    
    compile (group: 'org.antlr', name: 'antlr4', version: '4.2')
    compile (group: 'org.antlr', name: 'antlr4', version: '4.2', classifier: 'sources')

}
